dnl $Id$
dnl
dnl configure.in - autoconf input template to produce ./configure script
dnl
dnl version 2.52 is required for Cygwin libtool support
AC_PREREQ([2.52])

dnl local vars to hold user's preferences --------------------------------
AC_INIT([source/headers/geos.h])
AC_CANONICAL_SYSTEM

AC_CONFIG_MACRO_DIR([macros])

dnl -- JTS_PORT: the version of JTS this release is bound to
JTS_PORT=1.10.0

dnl -- Version info for the CAPI
CAPI_INTERFACE_CURRENT=7
CAPI_INTERFACE_REVISION=1
CAPI_INTERFACE_AGE=6

dnl
dnl -- Release versions / C++ library SONAME will use these
dnl -- encoding ABI break at every release
dnl
VERSION_MAJOR=3
VERSION_MINOR=3
VERSION_PATCH=0
VERSION="$VERSION_MAJOR.$VERSION_MINOR.$VERSION_PATCH"

dnl CAPI_VERSION_MAJOR=$(($CAPI_INTERFACE_CURRENT-$CAPI_INTERFACE_AGE))
dnl the following should be more portable
CAPI_VERSION_MAJOR=`expr $CAPI_INTERFACE_CURRENT - $CAPI_INTERFACE_AGE`
CAPI_VERSION_MINOR=$CAPI_INTERFACE_AGE
CAPI_VERSION_PATCH=$CAPI_INTERFACE_REVISION
CAPI_VERSION="$CAPI_VERSION_MAJOR.$CAPI_VERSION_MINOR.$CAPI_VERSION_PATCH"

AM_INIT_AUTOMAKE([geos], [$VERSION], [no-define])
AM_CONFIG_HEADER([source/headers/config.h])
AM_CONFIG_HEADER([source/headers/geos/platform.h])
AC_PROG_CC

dnl use libtool ----------------------------------------------------------
AC_LIBTOOL_DLOPEN
AC_LIBTOOL_WIN32_DLL
AC_PROG_LIBTOOL

dnl check for programs ----------------------------------------------------
AC_PROG_CXX
AC_ISC_POSIX
AC_PROG_INSTALL
AC_PROG_MAKE_SET

dnl function checks ------------------------------------------------------
AC_FUNC_CLOSEDIR_VOID
AC_FUNC_MEMCMP
AC_FUNC_STRFTIME
AC_FUNC_VPRINTF
AC_FUNC_ALLOCA
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_CHECK_HEADERS([memory.h])
AC_CHECK_HEADERS([unistd.h])
AC_CHECK_HEADERS([ieeefp.h])
AC_CHECK_HEADERS([sys/file.h])
AC_CHECK_HEADERS([sys/time.h])
AC_CHECK_FUNCS([strchr memcpy gettimeofday])
AC_HEADER_STAT
AC_STRUCT_TM
AC_TYPE_SIZE_T
AC_C_CONST

dnl --------------------------------------------------------------------
dnl - Check for inline and cassert settings
dnl --------------------------------------------------------------------


AC_ARG_ENABLE([inline], [  --disable-inline    Disable inlining],
	[case "${enableval}" in
		yes) enable_inline=true ;;
		no)  enable_inline=false ;;
		*) AC_MSG_ERROR(bad value ${enableval} for --enable-inline);;
	esac],
	[enable_inline=true]
)

AC_ARG_ENABLE([cassert], [  --disable-cassert   Disable assertion checking],
	[case "${enableval}" in
		yes) enable_cassert=true ;;
		no)  enable_cassert=false ;;
		*) AC_MSG_ERROR(bad value ${enableval} for --enable-cassert);;
	esac],
	[enable_cassert=true]
)

INLINE_FLAGS=
AC_SUBST(INLINE_FLAGS)
if test x"$enable_inline" = xtrue; then
	INLINE_FLAGS="-DGEOS_INLINE"
	CXXFLAGS="$CXXFLAGS $INLINE_FLAGS"
fi

if test x"$enable_cassert" = xfalse; then
	CXXFLAGS="$CXXFLAGS -DNDEBUG"
fi

dnl --------------------------------------------------------------------
dnl - Append default C++ and C flags 
dnl --------------------------------------------------------------------

dnl In order for AC_LIBTOOL_COMPILER_OPTION to use
dnl the C compiler we need the hack below.
dnl It is likely a bug in the libtool macro file to
dnl require AC_LIBTOOL_LANG_CXX_CONFIG in *addition*
dnl to AC_LANG(CXX) or AC_LANG_PUSH(CXX)/AC_LANG_POP()
dnl 
AC_LIBTOOL_LANG_CXX_CONFIG

# Set default CXXFLAGS and CFLAGS if not set by the user
# -pedantic: ISO does not support long long
# we add -Wno-long-long to avoid those messages
WARNFLAGS=""
AC_LIBTOOL_COMPILER_OPTION([if $compiler supports -pedantic], [dummy_cv_pedantic], [-pedantic], [], [WARNFLAGS="$WARNFLAGS -pedantic"], [])
AC_LIBTOOL_COMPILER_OPTION([if $compiler supports -Wall], [dummy_cv_wall], [-Wall], [], [WARNFLAGS="$WARNFLAGS -Wall"], [])
AC_LIBTOOL_COMPILER_OPTION([if $compiler supports -ansi], [dummy_cv_ansi], [-ansi], [], [WARNFLAGS="$WARNFLAGS -ansi"], [])
AC_LIBTOOL_COMPILER_OPTION([if $compiler supports -Wno-long-long], [dummy_cv_no_long_long], [-Wno-long-long], [], [WARNFLAGS="$WARNFLAGS -Wno-long-long"], [])

# To make numerical computation more stable, we use --ffloat-store
NUMERICFLAGS=""
AC_LIBTOOL_COMPILER_OPTION([if $compiler supports -ffloat-store], [dummy_cv_ffloat_store], [-ffloat-store], [], [NUMERICFLAGS="$NUMERICFLAGS -ffloat-store"], [])

DEFAULTFLAGS="${WARNFLAGS} ${NUMERICFLAGS}"

CXXFLAGS="${CXXFLAGS} ${DEFAULTFLAGS}"
CFLAGS="${CFLAGS} ${DEFAULTFLAGS}"

dnl --------------------------------------------------------------------
dnl - Look for finite and/or isfinite macros/functions
dnl --------------------------------------------------------------------

dnl These two tests need the math library or they won't link
dnl on OpenBSD, even if the functions exist.
save_LIBS=$LIBS
LIBS="$LIBS -lm"
AC_CACHE_CHECK([for finite], ac_cv_finite,
 [AC_TRY_LINK([#include <math.h>],
 [double x; int y; y = finite(x);],
 ac_cv_finite=yes,
 ac_cv_finite=no
)])
if test x"$ac_cv_finite" = x"yes"; then
  AC_DEFINE(HAVE_FINITE, [1], [Has finite])
fi

AC_CACHE_CHECK([for isfinite], ac_cv_isfinite,
 [AC_TRY_LINK([#include <math.h>],
 [double x; int y; y = isfinite(x);],
 ac_cv_isfinite=yes,
 ac_cv_isfinite=no
)])
if test x"$ac_cv_isfinite" = x"yes"; then
  AC_DEFINE(HAVE_ISFINITE, [1], [Has isfinite])
fi

dnl --------------------------------------------------------------------
dnl Test for presence of isnan function when using C++ and <cmath>
dnl This is for a particular bug in OS/X where <cmath> drops the definition
dnl of isnan().

AC_LANG_PUSH([C++])
AC_CACHE_CHECK([for isnan], ac_cv_isnan,
 [AC_TRY_LINK([#include <cmath>],
 [double x; int y; y = isnan(x);],
 ac_cv_isnan=yes,
 ac_cv_isnan=no
)])
if test x"$ac_cv_isnan" = x"yes"; then
  AC_DEFINE(HAVE_ISNAN, [1], [Has isnan])
fi
AC_LANG_POP([C++])

dnl --------------------------------------------------------------------

LIBS=$save_LIBS

dnl --------------------------------------------------------------------
dnl - Look for a 64bit integer (do after CFLAGS is set)
dnl --------------------------------------------------------------------

dnl Find a working 64bit integer
PGAC_TYPE_64BIT_INT([int64_t])
if test x"$HAVE_INT64_T_64" = x"no" ; then
  PGAC_TYPE_64BIT_INT([long int])
  if test x"$HAVE_LONG_INT_64" = x"no" ; then
    PGAC_TYPE_64BIT_INT([long long int])
    if test x"$HAVE_LONG_LONG_INT_64" = x"no" ; then
      AC_MSG_WARN([Could not find a working 64bit int type, you may experience weird bugs (undefined behaviour)]);
    fi
  fi
fi



dnl --------------------------------------------------------------------
dnl - check whether python is required for the build
dnl --------------------------------------------------------------------

AC_ARG_ENABLE([python], [  --enable-python    Enable build of python module],
	[case "${enableval}" in
		yes) use_python=true ;;
		no)  use_python=false ;;
		*) AC_MSG_ERROR(bad value ${enableval} for --enable-python) ;;
	esac],
	[use_python=false]
)


dnl --------------------------------------------------------------------
dnl - check whether ruby is required for the build
dnl --------------------------------------------------------------------

AC_ARG_ENABLE([ruby], [  --enable-ruby    Enable build of ruby module],
	[case "${enableval}" in
		yes) use_ruby=true ;;
		no)  use_ruby=false ;;
		*) AC_MSG_ERROR(bad value ${enableval} for --enable-ruby) ;;
	esac],
	[use_ruby=false]
)


dnl --------------------------------------------------------------------
dnl - check for swig if python or ruby are enabled
dnl --------------------------------------------------------------------

use_swig=false
if test x"$use_python" = xtrue || test x"$use_ruby" = xtrue; then
	AC_PROG_SWIG(1.3.28)
	if test x"$SWIG" != "x"; then
	    SWIG_ENABLE_CXX	
            AC_SUBST(SWIG)           
 
            use_swig=true 
	fi
fi
AM_CONDITIONAL(ENABLE_SWIG, [ test x"$use_swig" = xtrue ])


dnl --------------------------------------------------------------------
dnl - check for python if enabled
dnl --------------------------------------------------------------------

if test x"$use_python" = xtrue; then
	dnl Check for Python 
        AM_PATH_PYTHON	
        SWIG_PYTHON

	if test x"$PYTHON" = "x"; then
		use_python=false
	fi
	
	AC_SUBST(PYTHON)
	AC_SUBST(SWIG_PYTHON_CPPFLAGS)
	AC_SUBST(SWIG_PYTHON_OPT)
fi
AM_CONDITIONAL(ENABLE_PYTHON, [test x"$use_python" = xtrue])


dnl --------------------------------------------------------------------
dnl - check for ruby if enabled
dnl --------------------------------------------------------------------

if test x"$use_ruby" = xtrue; then
	dnl Check for Ruby 
	AC_RUBY_DEVEL

	if test x"$RUBY" = x; then
		use_ruby=false
	fi

fi
AM_CONDITIONAL(ENABLE_RUBY, [ test x"$use_ruby" = xtrue ])


dnl --------------------------------------------------------------------
dnl - do operating-system specific things
dnl --------------------------------------------------------------------

AC_MSG_CHECKING([OS-specific settings])

case "${host_os}" in
	*darwin*)
		AC_MSG_RESULT([${host_os}])
		AC_MSG_CHECKING([for OS/X version])
		kernel=`uname -r`

		# "Darwin 9.6.0" is Mac OSX 10.5.6
		# "Darwin 10.x" would presumably be Mac OS X 10.6.x
		case "${kernel}" in
			8.*)
				AC_MSG_RESULT([Mac OS X 10.4 Tiger])
				;;
			9.*)
				AC_MSG_RESULT([Mac OS X 10.5 Leopard])
				;;
			10.*)
dnl				CXXFLAGS="$CXXFLAGS -Wnon-virtual-dtor -Woverloaded-virtual"
				AC_MSG_RESULT([Mac OS X 10.6 Snow Leopard])
				;;
			*)
				AC_MSG_RESULT([Mac OS X (Darwin ${kernel} kernel)])
				;;
		esac
		;;
esac

dnl --------------------------------------------------------------------
dnl - check for boost 
dnl --------------------------------------------------------------------

dnl -- AX_BOOST(1.32)
dnl -- if test "x$ax_cv_boost_unit_test_framework" = "xyes"; then
dnl -- 	use_boost_utf=yes
dnl -- else
dnl -- 	use_boost_utf=no
dnl -- fi
dnl -- AM_CONDITIONAL(ENABLE_BOOST_UTF, [test "x$use_boost_utf" = "xyes"])

dnl things to substitute in output ----------------------------------------
AC_SUBST(VERSION)
AC_SUBST(VERSION_MAJOR)
AC_SUBST(VERSION_MINOR)
AC_SUBST(VERSION_PATCH)
AC_SUBST(INTERFACE_CURRENT)
AC_SUBST(INTERFACE_REVISION)
AC_SUBST(INTERFACE_AGE)
AC_SUBST(JTS_PORT)
AC_SUBST(CAPI_VERSION)
AC_SUBST(CAPI_VERSION_MAJOR)
AC_SUBST(CAPI_VERSION_MINOR)
AC_SUBST(CAPI_VERSION_PATCH)
AC_SUBST(CAPI_INTERFACE_CURRENT)
AC_SUBST(CAPI_INTERFACE_REVISION)
AC_SUBST(CAPI_INTERFACE_AGE)

dnl output stuff ----------------------------------------------------------

AC_OUTPUT([
	Makefile 
	build/Makefile
	build/msvc80/Makefile
	build/msvc80/geos_c_dll/Makefile
	build/msvc80/geos_lib/Makefile
	build/msvc80/geos_python/Makefile
	build/msvc80/geos_ruby/Makefile
	build/msvc80/geos_unit/Makefile
	build/msvc80/geos_xmltester/Makefile
	build/msvc90/Makefile
	build/msvc90/geos_c_dll/Makefile
	build/msvc90/geos_lib/Makefile
	build/msvc90/geos_python/Makefile
	build/msvc90/geos_ruby/Makefile
	build/msvc90/geos_unit/Makefile
	build/msvc90/geos_xmltester/Makefile
	build/msvc10/Makefile
	build/msvc10/geos_c_dll/Makefile
	build/msvc10/geos_lib/Makefile
	build/msvc10/geos_unit/Makefile
	build/msvc10/geos_xmltester/Makefile
	capi/Makefile
	capi/geos_c.h
	doc/Doxyfile
	doc/Makefile
	macros/Makefile
	source/Makefile 
	source/algorithm/Makefile
	source/algorithm/locate/Makefile
	source/algorithm/distance/Makefile
	source/geom/Makefile
	source/geom/prep/Makefile
	source/geom/util/Makefile
	source/geomgraph/Makefile
	source/geomgraph/index/Makefile
	source/headers/Makefile
	source/headers/geos/Makefile
	source/headers/geos/algorithm/Makefile
	source/headers/geos/algorithm/locate/Makefile
	source/headers/geos/algorithm/distance/Makefile
	source/headers/geos/geom/Makefile
	source/headers/geos/geom/prep/Makefile
	source/headers/geos/geom/util/Makefile
	source/headers/geos/geomgraph/Makefile
	source/headers/geos/geomgraph/index/Makefile
	source/headers/geos/index/Makefile
	source/headers/geos/index/bintree/Makefile
	source/headers/geos/index/chain/Makefile
	source/headers/geos/index/intervalrtree/Makefile
	source/headers/geos/index/quadtree/Makefile
	source/headers/geos/index/strtree/Makefile
	source/headers/geos/index/sweepline/Makefile
	source/headers/geos/io/Makefile
	source/headers/geos/linearref/Makefile
	source/headers/geos/noding/Makefile
	source/headers/geos/noding/snapround/Makefile
	source/headers/geos/operation/Makefile
	source/headers/geos/operation/buffer/Makefile
	source/headers/geos/operation/distance/Makefile
	source/headers/geos/operation/linemerge/Makefile
	source/headers/geos/operation/overlay/Makefile
	source/headers/geos/operation/overlay/snap/Makefile
	source/headers/geos/operation/polygonize/Makefile
	source/headers/geos/operation/predicate/Makefile
	source/headers/geos/operation/relate/Makefile
	source/headers/geos/operation/union/Makefile
	source/headers/geos/operation/valid/Makefile
	source/headers/geos/planargraph/Makefile
	source/headers/geos/planargraph/algorithm/Makefile
	source/headers/geos/precision/Makefile
	source/headers/geos/simplify/Makefile
	source/headers/geos/util/Makefile
	source/headers/geos/version.h
	source/index/Makefile
	source/index/bintree/Makefile
	source/index/chain/Makefile
	source/index/intervalrtree/Makefile
	source/index/quadtree/Makefile
	source/index/strtree/Makefile
	source/index/sweepline/Makefile
	source/io/Makefile
	source/linearref/Makefile
	source/noding/Makefile
	source/noding/snapround/Makefile
	source/operation/Makefile
	source/operation/buffer/Makefile
	source/operation/distance/Makefile
	source/operation/linemerge/Makefile
	source/operation/overlay/Makefile
	source/operation/polygonize/Makefile
	source/operation/predicate/Makefile
	source/operation/relate/Makefile
	source/operation/union/Makefile
	source/operation/valid/Makefile
	source/planargraph/Makefile
	source/precision/Makefile
	source/simplify/Makefile
	source/util/Makefile
	swig/geos.i
	swig/Makefile
	swig/python/Makefile
	swig/python/tests/Makefile
	swig/ruby/Makefile
	swig/ruby/test/Makefile
	tests/Makefile
	tests/bigtest/Makefile
	tests/unit/Makefile
	tests/perf/Makefile
	tests/perf/operation/Makefile
	tests/perf/operation/buffer/Makefile
	tests/xmltester/Makefile
	tests/geostest/Makefile
	tests/thread/Makefile
	tools/Makefile
	tools/geos-config
	])

dnl -- echo "---------------------------------------"
dnl -- echo "Boost UTF: $use_boost_utf"
echo "Swig: $use_swig"
echo "Python: $use_python"
echo "Ruby: $use_ruby"
dnl -- echo "---------------------------------------"
